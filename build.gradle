buildscript {
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

plugins {
	id 'java'
	id 'eclipse'
	id 'jacoco'
	id 'com.github.spotbugs' version '1.6.4'
}

repositories {
	jcenter()
}

dependencies {
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
	testCompile 'org.junit.jupiter:junit-jupiter-params:5.2.0'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
	testImplementation 'org.hamcrest:hamcrest-library:1.3'
	compile 'net.jcip:jcip-annotations:1.0'
	compile 'com.github.spotbugs:spotbugs-annotations:3.1.3'
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    
    jacocoTestReport
}

jacocoTestReport {

    reports {

        xml.enabled true

        html.enabled false

    }

}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

eclipse.project {
	natures 'org.eclipse.jdt.core.javanature'
  	natures 'org.eclipse.buildship.core.gradleprojectnature'
}

build.finalizedBy(jacocoTestReport)

apply plugin: 'com.novoda.bintray-release'

publish {
	publishVersion = '4.1.0'
	repoName = 'MyObjects'
    userOrg = 'pomeryt'
    groupId = 'pomeryt.myobjects'
    artifactId = 'myobjects'
    desc = 'https://github.com/pomeryt/MyObjects'
    website = 'https://github.com/pomeryt/MyObjects'
}

spotbugs {
	toolVersion = '3.1.3'
	ignoreFailures = false
	effort = 'max'
	excludeFilter = file("$rootProject.projectDir/config/spotbugs/exclude.xml")
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
	reports {
		xml.enabled false
		html.enabled true
	}
}

apply plugin: 'pmd'
pmd {
	toolVersion = '5.1.2'
	ignoreFailures = false
	ruleSets = [
		'java-basic',
		'java-braces',
		'java-clone',
		'java-empty',
		'java-finalizers',
		'java-imports',
		'java-junit',
		'java-naming',
		'java-strings',
		'java-typeresolution',
		'java-unnecessary',
		'java-unusedcode'
	]
}
